import os
import numpy as np

ROOT = os.path.abspath('../../..')

import sys

sys.path.append(ROOT)
from scripts.snakemake_module import *

EXPERIMENT = os.path.abspath('.')
os.makedirs(EXPERIMENT + '/merge', exist_ok=True)

configfile: open_config(EXPERIMENT, 'config.yaml')

COMPILE = config['EXPERIMENT']['COMPILE']
PREFERENCES = copy_params(EXPERIMENT, ROOT, config['SIMULATION']['PREFERENCES'])

#Â Parameters for the simulation
SIMULATION_PARAMS = ' --nuc_matrix ' + copy_params(EXPERIMENT, ROOT, config['SIMULATION']['NUC_MATRIX'])
SIMULATION_PARAMS += ' --precision_matrix ' + copy_params(EXPERIMENT, ROOT, config['SIMULATION']['PRECISION_MATRIX'])
if config['SIMULATION']['BRANCH_WISE_CORRELATION']:
    SIMULATION_PARAMS += ' --branch_wise_correlation'
if config['SIMULATION']['FIXED_POP_SIZE']:
    SIMULATION_PARAMS += ' --fix_pop_size'
if config['SIMULATION']['FIXED_MUT_RATE']:
    SIMULATION_PARAMS += ' --fix_mut_rate'
if config['SIMULATION']['FIXED_GEN_TIME']:
    SIMULATION_PARAMS += ' --fix_gen_time'

SIMULATION_SIMUMODE_PARAM = {"SimuDiv": '--preferences ' + PREFERENCES, "SimuPoly": '--preferences ' + PREFERENCES,
                             "SimuFold": '--pdb_folder {0}/{1}'.format(ROOT, config['SimuFold']['PDB_FOLDER']),
                             "SimuStab": '', "SimuGeo": '', "SIMULATION": SIMULATION_PARAMS}

excluded_params = set()
for p_name in ['X_PARAM', 'Y_PARAM', 'X_PARAM_REVERSE', 'Y_PARAM_REVERSE']:
    excluded_params.update(set(config['EXPERIMENT'][p_name].keys()))

for simulator in SIMULATION_SIMUMODE_PARAM.keys():
    for param, value in config[simulator].items():
        if param in excluded_params or type(value)==bool:
            continue
        try:
            float(value)
            SIMULATION_SIMUMODE_PARAM[simulator] += ' --{0} {1}'.format(param.lower(), value)
        except:
            pass

SIMULATION_PARAMS = SIMULATION_SIMUMODE_PARAM["SIMULATION"]

diff_git_dir("{0}/SimuEvol".format(ROOT), "{0}/SimuEvol".format(EXPERIMENT))

SIMULATORS = config["EXPERIMENT"]["SIMULATORS"]
X_SCALE = ["{0:.3g}".format(i) for i in
           np.logspace(-config["EXPERIMENT"]["X_PARAM_MAGNITUDE"] / 2, config["EXPERIMENT"]["X_PARAM_MAGNITUDE"] / 2,
                       config["EXPERIMENT"]["HEATMAP_GRID_STEP"])]
Y_SCALE = ["{0:.3g}".format(i) for i in
           np.logspace(-config["EXPERIMENT"]["Y_PARAM_MAGNITUDE"] / 2, config["EXPERIMENT"]["Y_PARAM_MAGNITUDE"] / 2,
                       config["EXPERIMENT"]["HEATMAP_GRID_STEP"])]
np.random.seed(seed=0)
REPLICATES = np.random.choice(9999, size=config["EXPERIMENT"]["REPLICATES"], replace=False)

localrules: all, make_simuevol, build, merge_seed, plot_heatmap, all_heatmap

rule all:
    input:
         EXPERIMENT + '/all_heatmap'

rule make_simuevol:
    output: [EXPERIMENT + "/" + n for n in SIMULATORS]
    input: dir=EXPERIMENT + '/SimuEvol.version'
    params: compile="&& make clean && make" if COMPILE else ""
    log: out=EXPERIMENT + '/std.SimuEvol.stdout', err=EXPERIMENT + '/std.SimuEvol.stderr'
    shell:'cd {ROOT}/SimuEvol {params.compile} 2> {log.err} 1> {log.out} ' + ' '.join(['&& cp build/{0} {1}'.format(n, EXPERIMENT) for n in SIMULATORS])

rule build:
    input: [EXPERIMENT + "/" + n for n in SIMULATORS]

def get_scaled_value(w, axis):
    list_params = []

    def extract(p):
        return float(config[w.simumode][p] if p in config[w.simumode] else config["SIMULATION"][p])

    def append(p, p_type, v):
        list_params.append("--{0} {1}".format(p.lower(), int(v) if p_type == 'int' else v))

    for p, p_type in config['EXPERIMENT'][axis.upper() + "_PARAM"].items():
        v = extract(p) * float(getattr(w, axis))
        append(p, p_type, v)
    for p, p_type in config['EXPERIMENT'][axis.upper() + "_PARAM_REVERSE"].items():
        v = extract(p) / float(getattr(w, axis))
        append(p, p_type, v)
    return " ".join(list_params)

rule run_simulation:
    output: touch(EXPERIMENT + '/{simumode}_{x}_{y}/{seed}_exp')
    input:
         exec=EXPERIMENT + '/{simumode}',
         config_core=EXPERIMENT + '/config.SIMULATION',
         config_pan=EXPERIMENT + '/config.' + '{simumode}',
         prefs=PREFERENCES
    params:
          time="3-23:00", mem=4000, threads=1,
          pan=lambda wildcards: SIMULATION_SIMUMODE_PARAM[wildcards.simumode],
          x=lambda wildcards: get_scaled_value(wildcards, 'x'),
          y=lambda wildcards: get_scaled_value(wildcards, 'y'),
          seed=lambda wildcards: "--seed {0}".format(wildcards.seed),
          dir=lambda wildcards: EXPERIMENT + '/{0}_{1}_{2}'.format(wildcards.simumode, wildcards.x, wildcards.y)
    log: out=EXPERIMENT + '/{simumode}_{x}_{y}/std.{seed}_exp.stdout',
       err=EXPERIMENT + '/{simumode}_{x}_{y}/std.{seed}_exp.stdout'
    shell:
         'mkdir -p {params.dir} && {input.exec} {SIMULATION_PARAMS} {params.pan} {params.x} {params.y} {params.seed} --output {output} 2> {log.err} 1> {log.out}'

rule merge_seed:
    output: plot=EXPERIMENT + '/merge/{simumode}_{x}_{y}_seed.tsv'
    input:
         src=ROOT + "/scripts/scaling_merge.py",
         simu=expand(EXPERIMENT + '/{{simumode}}_{{x}}_{{y}}/{seed}_exp', seed=REPLICATES)
    shell: 'python3 {input.src} --input {input.simu} --output {output.plot}'

def get_axis_name(axis):
    std_list = list(config['EXPERIMENT'][axis.upper() + "_PARAM"].keys())
    std_list += ["1/" + k for k in config['EXPERIMENT'][axis.upper() + "_PARAM_REVERSE"].keys()]
    return ", ".join(std_list)

rule plot_heatmap:
    output: plot=directory(EXPERIMENT + '/heatmap_{simumode}_plot')
    input:
         src=ROOT + "/scripts/heatmap_analysis.py",
         simu=expand(EXPERIMENT + '/merge/{{simumode}}_{x}_{y}_seed.tsv', x=X_SCALE, y=Y_SCALE)
    params:
          x=get_axis_name("x"), y=get_axis_name("y")
    shell: 'mkdir -p {output.plot} && python3 {input.src} --input {input.simu} --output {output.plot} --x_param "{params.x}" --y_param "{params.y}"'

rule all_heatmap:
    output: touch(EXPERIMENT + '/all_heatmap')
    input:
         expand(EXPERIMENT + '/heatmap_{simumode}_plot', simumode=SIMULATORS)

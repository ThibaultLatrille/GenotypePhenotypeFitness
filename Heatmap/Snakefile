import os
import numpy as np
import itertools
import sys

ROOT = os.path.abspath('../../..')
sys.path.append(ROOT)
EXPERIMENT = os.path.abspath('.')
os.makedirs(EXPERIMENT + '/merge', exist_ok=True)
os.makedirs(EXPERIMENT + '/merge_all', exist_ok=True)
from scripts.snakemake_module import *

configfile: open_config(EXPERIMENT, 'config.yaml')

#Â Parameters for the simulation
COMPILE = config['EXPERIMENT']['COMPILE']
PREFERENCES = copy_params(EXPERIMENT, ROOT, config['SIMULATION']['PREFERENCES'])
simulation_params = ' --nuc_matrix ' + copy_params(EXPERIMENT, ROOT, config['SIMULATION']['NUC_MATRIX'])
simulation_params += ' --precision_matrix ' + copy_params(EXPERIMENT, ROOT, config['SIMULATION']['PRECISION_MATRIX'])
SIMUMODE_PARAM = {"SimuProfile": '--preferences ' + PREFERENCES, "PolyProfile": '--preferences ' + PREFERENCES,
                  "SimuFold": '--pdb_folder {0}/{1}'.format(ROOT, config['SimuFold']['PDB_FOLDER']),
                  "SimuStab": '', "SimuGeo": '', "SimuDfe": '', "ToyStab": '', "ToyGeo": '',
                  "SIMULATION": simulation_params, "TOY": ''}

excluded_params = set()
for p_name in ['X_PARAM', 'Y_PARAM', 'X_PARAM_REVERSE', 'Y_PARAM_REVERSE']:
    excluded_params.update(set(config['EXPERIMENT'][p_name].keys()))

for simulator in SIMUMODE_PARAM.keys():
    for param, value in config[simulator].items():
        if type(value) == bool:
            if value:
                SIMUMODE_PARAM[simulator] += ' --{0}'.format(param.lower())
        elif param not in excluded_params:
            try:
                float(value)
                SIMUMODE_PARAM[simulator] += ' --{0} {1}'.format(param.lower(), value)
            except:
                pass

diff_git_dir("{0}/SimuEvol".format(ROOT), "{0}/SimuEvol".format(EXPERIMENT))

SIMULATORS = config["EXPERIMENT"]["SIMULATORS"]
X_SCALE = ["{0:.3g}".format(i) for i in
           np.logspace(-config["EXPERIMENT"]["X_PARAM_MAGNITUDE"] / 2, config["EXPERIMENT"]["X_PARAM_MAGNITUDE"] / 2,
                       config["EXPERIMENT"]["X_PARAM_STEPS"])]
y_multiply = not ("Y_PARAM_SCALE" in config["EXPERIMENT"] and "linear" == config["EXPERIMENT"]["Y_PARAM_SCALE"])
if y_multiply:
    Y_SCALE = ["{0:.3g}".format(i) for i in
           np.logspace(-config["EXPERIMENT"]["Y_PARAM_MAGNITUDE"] / 2, config["EXPERIMENT"]["Y_PARAM_MAGNITUDE"] / 2,
                       config["EXPERIMENT"]["Y_PARAM_STEPS"])]
else:
    Y_SCALE = ["{0:.3g}".format(i) for i in
           np.linspace(-config["EXPERIMENT"]["Y_PARAM_MAGNITUDE"] / 2, config["EXPERIMENT"]["Y_PARAM_MAGNITUDE"] / 2,
                       config["EXPERIMENT"]["Y_PARAM_STEPS"])]
np.random.seed(seed=0)
REPLICATES = np.random.choice(9999, size=config["EXPERIMENT"]["REPLICATES"], replace=False)

for x, y, simumode in itertools.product(X_SCALE, Y_SCALE, SIMULATORS):
    os.makedirs(EXPERIMENT + '/{simumode}_{x}_{y}'.format(simumode=simumode, x=x, y=y), exist_ok=True)

localrules: all, make_simuevol, build, merge_seed, plot_heatmap

print(config["SIMULATION"]["STEP_WISE_POP_SIZE"] == True)
print(config["SIMULATION"]["STEP_WISE_POP_SIZE"])
rule all:
    input:
         EXPERIMENT + '/merge_all/Stability.pdf' if (config["SIMULATION"]["STEP_WISE_POP_SIZE"] == True or len(X_SCALE) <= 1)
          else expand(EXPERIMENT + '/heatmap_{simumode}_plot',  simumode=SIMULATORS)

rule make_simuevol:
    output: [EXPERIMENT + "/" + n for n in SIMULATORS]
    input: dir=EXPERIMENT + '/SimuEvol.version'
    params: compile="&& make clean && make" if COMPILE else ""
    log: out=EXPERIMENT + '/std.SimuEvol.stdout', err=EXPERIMENT + '/std.SimuEvol.stderr'
    shell:'cd {ROOT}/SimuEvol {params.compile} 2> {log.err} 1> {log.out} ' + \
         ' '.join(['&& cp build/{0} {1}'.format(n, EXPERIMENT) for n in SIMULATORS])

rule build:
    input: [EXPERIMENT + "/" + n for n in SIMULATORS]


def extract(p, simumode):
    return float(config[simumode][p] if p in config[simumode] else config["SIMULATION"][p])


def append(list_params, p, p_type, v):
    list_params.append("--{0} {1}".format(p.lower(), round(v) if p_type == 'int' else v))


def get_scaled_value(w, axis, s, multiply=True):
    list_params = []

    for p, p_type in config['EXPERIMENT'][axis.upper() + "_PARAM"].items():
        v = extract(p, s) * float(getattr(w, axis)) if multiply else extract(p, s) + float(getattr(w, axis))
        append(list_params, p, p_type, v)
    for p, p_type in config['EXPERIMENT'][axis.upper() + "_PARAM_REVERSE"].items():
        v = extract(p, s) / float(getattr(w, axis)) if multiply else extract(p, s) - float(getattr(w, axis))
        append(list_params, p, p_type, v)
    return " ".join(list_params)


rule run_simulation:
    output:
          touch(EXPERIMENT + '/{simumode}_{x}_{y}/{seed}_exp')
    input:
         exec=EXPERIMENT + '/{simumode}',
         config_core=EXPERIMENT + '/config.SIMULATION',
         config_pan=EXPERIMENT + '/config.' + '{simumode}',
         prefs=PREFERENCES
    params:
          time="3-23:00", mem=4000, threads=1,
          core=lambda wildcards: SIMUMODE_PARAM["TOY"] if ("Toy" in wildcards.simumode) else SIMUMODE_PARAM[
              "SIMULATION"],
          pan=lambda wildcards: SIMUMODE_PARAM[wildcards.simumode],
          x=lambda wildcards: get_scaled_value(wildcards, 'x', wildcards.simumode),
          y=lambda wildcards: get_scaled_value(wildcards, 'y', wildcards.simumode, y_multiply),
          seed=lambda wildcards: "--seed {0}".format(wildcards.seed),
          dir=lambda wildcards: EXPERIMENT + '/{0}_{1}_{2}'.format(wildcards.simumode, wildcards.x, wildcards.y)
    log: out=EXPERIMENT + '/{simumode}_{x}_{y}/std.{seed}_exp.stdout',
       err=EXPERIMENT + '/{simumode}_{x}_{y}/std.{seed}_exp.stdout'
    shell:
         'mkdir -p {params.dir} && {input.exec} {params.core} {params.pan} {params.x} {params.y} {params.seed} --output {output} 2> {log.err} 1> {log.out}'

rule merge_seed:
    output: plot=EXPERIMENT + '/merge/{simumode}_{x}_{y}_seed.tsv'
    input:
         src=ROOT + "/scripts/scaling_merge.py",
         simu=expand(EXPERIMENT + '/{{simumode}}_{{x}}_{{y}}/{seed}_exp', seed=REPLICATES),
         input=EXPERIMENT + '/{simumode}_{x}_{y}'
    params:
          fitting=" --fitting True" if config["SIMULATION"]["BIAS_POP_SIZE"] != 0 else "",
          distrib=" --distrib True" if "PLOT_DISTRIBUTION" in config["EXPERIMENT"] and config["EXPERIMENT"]["PLOT_DISTRIBUTION"] else "",
    shell: 'python3 {input.src} --input {input.input} --output {output.plot}{params.fitting}{params.distrib}'

rule plot_heatmap:
    output: plot=directory(EXPERIMENT + '/heatmap_{simumode}_plot')
    input:
         src=ROOT + "/scripts/heatmap_analysis.py",
         simu=expand(EXPERIMENT + '/merge/{{simumode}}_{x}_{y}_seed.tsv', x=X_SCALE, y=Y_SCALE)
    params:
          node=" --node True" if config["SIMULATION"]["BIAS_POP_SIZE"] != 0 else "",
    shell:
         "mkdir -p {output.plot} && python3 {input.src}{params.node} --input {input.simu} --output {output.plot}"

rule merge_all:
    output: plot=EXPERIMENT + '/merge_all/Stability.pdf'
    input:
         src=ROOT + "/scripts/relaxation_merge.py",
         merge=expand(EXPERIMENT + '/merge/{simumode}_{x}_{y}_seed.tsv', simumode=SIMULATORS, x=X_SCALE, y=Y_SCALE),
         folder=expand(EXPERIMENT + '/{simumode}_{x}_{y}', simumode=SIMULATORS, x=X_SCALE, y=Y_SCALE)
    params: "--age {0} --branches {1}".format(config['SIMULATION']['ROOT_AGE'], config['SIMULATION']['NBR_BRANCHES'])
    shell: "python3 {input.src} --input {input.folder} --output {output.plot} {params}"

print("Simulators={0}".format(SIMULATORS))
if "SimuStab" in SIMULATORS:
    print("Grantham={0}".format(config['SimuStab']['GRANTHAM']))
    print("Alpha={0:.3g}".format(config['SimuStab']['ALPHA']))
    print("Gamma={0:.3g}".format(config['SimuStab']['GAMMA']))
    if 'GAMMA_STD' in config['SimuStab']:
        print("GammaStd={0:.3g}".format(config['SimuStab']['GAMMA_STD']))
    print("1/(beta*n*gamma)={0:.3g}".format(
        1.0 / (config['SimuStab']['BETA'] * config['SIMULATION']['EXON_SIZE'] * config['SimuStab']['GAMMA'])))
